/* CHANGELOG
 * Written by Josh Grooms on 20160903
 */



/** DATA **/
struct Luminance
{
    vec4 Ambient;
    vec4 Diffuse;
    vec4 Specular;
};

struct Illuminance
{
    vec4 Ambient;
    vec4 Diffuse;
    vec4 Emissive;
    vec4 Specular;
};



/** UTILITIES **/
float Lambert(vec3 normal, vec3 toLight, float ambience)
{
    return max(dot(normal, toLight), ambience);
}
float RimLight(vec3 normal, vec3 toEye, float power)
{
    float f = 1.0 - dot(normal, toEye);
    return pow(smoothstep(0.0, 1.0, f), power);
}
float SpecularPhong(vec3 normal, vec3 toLight, vec3 toEye, float power)
{
    vec3 reflection = reflect(-toLight, normal);
    return pow(max( dot(reflection, toEye), 0.0 ), power);
}
float SpecularBlinnPhong(vec3 normal, vec3 toLight, vec3 toEye, float power)
{
    vec3 halfway = normalize(toLight + toEye);
    return pow(max( dot(normal, halfway), 0.0 ), power);
}
